# ERC-4907

## Specification

The [ERC-4907](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-4907.md) standard is an extension of 
[ERC-721](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-721.md). It defines a simple interface to enable NFT
owners to set a new role, called `user`,  that can use the NFT for a pre-defined period of time. This new role allows
owners to lend their NFTs without compromising on custody, opening the door for multiple use cases.

The interface below lists all new events and functions included in this standard. Please note that every ERC-4907 
compliant contract must implement the ERC-721 interface.

```solidity
pragma solidity ^0.8.9;

import { IERC721 } from "../contracts/interfaces/IERC721.sol";

interface IERC4907 is IERC721 {
    // Logged when the user of an NFT is changed or expires is changed
    /// @notice Emitted when the `user` of an NFT or the `expires` of the `user` is changed
    /// The zero address for user indicates that there is no user address
    event UpdateUser(uint256 indexed tokenId, address indexed user, uint64 expires);

    /// @notice set the user and expires of an NFT
    /// @dev The zero address indicates there is no user
    /// Throws if `tokenId` is not valid NFT
    /// @param user  The new user of the NFT
    /// @param expires  UNIX timestamp, The new user could use the NFT before expires
    function setUser(
        uint256 tokenId,
        address user,
        uint64 expires
    ) external;

    /// @notice Get the user address of an NFT
    /// @dev The zero address indicates that there is no user or the user is expired
    /// @param tokenId The NFT to get the user address for
    /// @return The user address for this NFT
    function userOf(uint256 tokenId) external view returns (address);

    /// @notice Get the user expires of an NFT
    /// @dev The zero value indicates that there is no user
    /// @param tokenId The NFT to get the user expires for
    /// @return The user expires for this NFT
    function userExpires(uint256 tokenId) external view returns (uint256);
}
```

### Monitoring User Rights Assignment

In order to find out which user holds access rights for an NFT, developers can either:

- Call view functions to return the desired information. `userOf(uint256 tokenId)` should return the user address, and
`userExpires(uint256 tokenId)` should return the expiration date. If the user is the zero address, or the 
expiration date is due, the user rights should be assigned to the NFT owner.
- Build a database to monitor the events. Each time `UpdateUser(uint256 tokenId, address user, uint64 expires)` is
emitted, the database must be updated with the new values and the expiration date.

### Caveats

- One can check if a contract implements `IERC4907` with ERC-165 `supportsInterface`.
- The `user` address can be any address, including the zero address. The zero address indicates that there is no user.
- If using the database approach, it's important to note that no event will be emitted when the user rights expires, so
it's necessary to build logic to automatically expire it at the expiration date.
